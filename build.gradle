plugins {
    id 'java-library'
    id 'net.nemerosa.versioning' version '3.0.0'
}

apply plugin: 'maven-publish'
group = 'org.kettingpowered'

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'net.nemerosa.versioning'

    repositories {
        maven {
            name 'c0d3m4513r'
            url 'https://nexus.c0d3m4513r.com/repository/Magma/'
        }
        maven {
            name 'MinecraftForge'
            url 'https://maven.minecraftforge.net/'
        }
    }

    dependencies {
        implementation 'org.jetbrains:annotations:24.0.0'
    }
}

dependencies {
    api 'org.slf4j:slf4j-api:1.8.0-beta4'
    implementation 'org.spongepowered:mixin:0.8.5'
}

jar {
    //archiveFileName = 'KettingCore.jar'
    
    manifest {
        attributes 'Specification-Title': 'Kettingpowered',
            'Specification-Vendor': 'Kettingpowered',
            'Implementation-Title': 'KettingCore',
            'Implementation-Version': System.getenv("VERSION") ?: 'dev-ver',
            'Implementation-Vendor': 'Kettingpowered'
    }

    from subprojects.collect { it.sourceSets.main.output }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}


publishing {
    publications {
      myPublication(MavenPublication) {
        from components.java
        artifact(sourcesJar) {
            // These values will be used instead of the values from the task. The task values will not be updated.
            classifier "sources"
            extension "jar"
        }
        artifact(javadocJar) {
             //These values will be used instead of the values from the task. The task values will not be updated.
            classifier "javadoc"
            extension "jar"
        }
//        artifact jar
//        groupId = "org.kettingpowered"
//        artifactId = "kettingcore"
        version = System.getenv("VERSION")
        pom {
          name = "KettingCore"
          description = "An API supported across all Ketting Versions"
          url = "https://github.com/kettingpowered/KettingCore"
          licenses {
            license {
              name = "MIT License"
              url = "http://www.opensource.org/licenses/mit-license.php"
            }
          }
          developers {
            developer {
              id = "justred23"
              name = "JustRed23"
              //email = "jon@doe"
            }
          }
          scm {
            connection = "scm:git:https://github.com/kettingpowered/KettingCore.git"
            //developerConnection = "scm:svn:https://subversion.example.com/svn/project/trunk/"
            url = "https://github.com/kettingpowered/KettingCore"
          }
        }
      }
    }
    repositories {
       maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/kettingpowered/KettingCore")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
       }
        maven {
            name = 'kettingRepo'
            credentials {
                username = System.getenv("KETTINGUSERNAME")
                password = System.getenv("KETTINGPASSWORD")
            }
            url = "https://nexus.c0d3m4513r.com/repository/Ketting/"
        }
    }
}
