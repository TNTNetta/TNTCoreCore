plugins {
    id 'java-library'
    id 'net.nemerosa.versioning' version '3.0.0'
}

group = 'org.kettingpowered'

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'net.nemerosa.versioning'
    apply plugin: 'maven-publish'

    repositories {
        maven {
            name 'c0d3m4513r'
            url 'https://nexus.c0d3m4513r.com/repository/Magma/'
        }
        maven {
            name 'MinecraftForge'
            url 'https://maven.minecraftforge.net/'
        }
    }

    dependencies {
        implementation 'org.jetbrains:annotations:24.0.0'
    }
}

dependencies {
    api 'org.slf4j:slf4j-api:1.8.0-beta4'
    implementation project(':common')
    implementation 'org.spongepowered:mixin:0.8.5'
}

jar {
    archiveFileName = 'KettingCore.jar'

    manifest {
        attributes 'Specification-Title': 'Kettingpowered',
            'Specification-Vendor': 'Kettingpowered',
            'Implementation-Title': 'KettingCore',
            'Implementation-Version': versioning.info.build,
            'Implementation-Vendor': 'Kettingpowered'
    }

    from subprojects.collect { it.sourceSets.main.output }
}
task sourceJar(type: Jar) {
  archiveClassifier = "sources"
}


publishing {
    publications {
      myPublication(MavenPublication) {
        from components.java
        artifact sourceJar
        artifact jar
        artifact source: sourceJar, classifier: 'src', extension: 'zip'
        groupId = "org.kettingpowered"
        artifactId = "kettingcore"
        version = System.getenv("VERSION")
        pom {
          name = "KettingCore"
          description = "An API supported across all Ketting Versions"
          url = "https://github.com/kettingpowered/KettingCore"
          licenses {
            license {
              name = "MIT License"
              url = "http://www.opensource.org/licenses/mit-license.php"
            }
          }
          developers {
            developer {
              id = "justred23"
              name = "JustRed23"
              //email = "jon@doe"
            }
          }
          scm {
            connection = "scm:git:https://github.com/kettingpowered/KettingCore.git"
            //developerConnection = "scm:svn:https://subversion.example.com/svn/project/trunk/"
            url = "https://github.com/kettingpowered/KettingCore"
          }
        }
      }
    }
    repositories {
       maven {
            url = uri("https://maven.pkg.github.com/C0D3-M4513R/KettingCore")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GHUSERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GHTOKEN")
            }
       }
        maven {
            name = 'kettingRepo'
            credentials {
                username = System.getenv("KETTINGUSERNAME")
                password = System.getenv("KETTINGPASSWORD")
            }
            url = "https://nexus.c0d3m4513r.com/repository/Ketting/"
        }
    }
}
